#Область ИнициализацияПеременных

&НаКлиенте
Перем ДокументБраузера;

&НаКлиенте
Перем ОкноБраузера;

&НаКлиенте
Перем РедакторJSON;

&НаКлиенте
Перем ФормаОжидания;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка) 
	JSON = Параметры.JSON;
	JSON = Обработки.JSONEditor.ПолучитьМакет("Макет").ПолучитьТекст();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)  
	
	ФормаОжидания = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, УникальныйИдентификатор);
	
	КаталогКомпоненты = ПолучитьИмяВременногоФайла();
	КаталогНаДиске = Новый Файл(КаталогКомпоненты);

	ДвоичныеДанные = ДвоичныеДанныеМакета();
	
	Чтение = Новый ЧтениеДанных(ДвоичныеДанные);
	
	Файл = Новый ЧтениеZipФайла(Чтение.ИсходныйПоток());
	Файл.ИзвлечьВсе(КаталогКомпоненты);
		
	Браузер = КаталогКомпоненты 
	          + ПолучитьРазделительПути() 
			  + "jsoneditor" 
			  + ПолучитьРазделительПути() 
			  + "index.html";

КонецПроцедуры        

&НаСервереБезКонтекста
Функция ДвоичныеДанныеМакета()	
	Возврат Обработки.JSONEditor.ПолучитьМакет("Бинарник");	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура БраузерДокументСформирован(Элемент)
	
	ДокументБраузера = Элемент.Документ;
    ОкноБраузера = ДокументБраузера.parentWindow;     
	Если ОкноБраузера = Неопределено Тогда
        ОкноБраузера = ДокументБраузера.defaultView; 
    КонецЕсли;
	
	Если ОкноБраузера = Неопределено Тогда
		Возврат;		
	КонецЕсли;   
	
	РедакторJSON = ОкноБраузера.Init(Отображение);
	РедакторJSON.setText(JSON);    
	
	ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаОжидания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображениеПриИзменении(Элемент)
	
	Если РедакторJSON <> Неопределено Тогда
		РедакторJSON.destroy();
	КонецЕсли;
	
	РедакторJSON = ОкноБраузера.Init(Отображение);
	РедакторJSON.setText(JSON);

КонецПроцедуры

#КонецОбласти
