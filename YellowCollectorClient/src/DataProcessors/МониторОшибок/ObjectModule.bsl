#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОписаниеПеременных 

Перем МенеджерМонитора;

#КонецОбласти		

#Область ПрограммныйИнтерфейс  

Функция ПолучитьДанныеМонитораИзКоллектора(Знач ПараметрыМонитора = Неопределено) Экспорт
	
	ПараметрыМонитораИзменяемые = ИзменяемыеПараметрыМонитораИзФиксированных(ПараметрыМонитора);
	ДанныеМонитора = ДанныеМонитораИзКоллектораШаблон(); 
	ПодготовитьМониторКРаботеПриНеобходимости(ПараметрыМонитораИзменяемые);
	
	#Область ДокументПоВсемОшибкам
	ЗаголовкиВсехОтчетов = ПолучитьЗаголовкиВсехОтчетовОбОшибках(ПараметрыМонитораИзменяемые);
	ДанныеМонитора["ДокументПоВсемОшибкам"] = СформироватьДокументПоВсемОшибкам(ЗаголовкиВсехОтчетов, ПараметрыМонитораИзменяемые);
	#КонецОбласти
	
	#Область СтатистикаПоОшибкамОбщая	
	СтатистикаПоОшибкамКакМассив = СтатистикаПоОшибкамОбщаяКакМассив(ПараметрыМонитораИзменяемые);
	ДанныеМонитора["ДокументСтатистикаПоВсемОшибкам"] = СформироватьДокументСтатистикаПоВсемОшибкам(СтатистикаПоОшибкамКакМассив, ПараметрыМонитораИзменяемые);
	#КонецОбласти
	
	Возврат ДанныеМонитора;
	
КонецФункции

Процедура ПодготовитьМониторКРаботеПриНеобходимости(ПараметрыМонитора) Экспорт
	
	Если МониторГотовКРаботе() Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыМонитораИзменяемые = ИзменяемыеПараметрыМонитораИзФиксированных(ПараметрыМонитора);
	
	ЗаполнитьМаппингыПолейДляВыводаОтчетовИФорм(ПараметрыМонитораИзменяемые);
	ПодготовитьМониторКРаботе(ПараметрыМонитораИзменяемые);	
		
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПодготовкаМонитораКРаботе

Функция ИзменяемыеПараметрыМонитораИзФиксированных(ПараметрыМонитора) 
	
	Если Тип("ФиксированнаяСтруктура") = ТипЗнч(ПараметрыМонитора) Тогда
		Возврат ОбщегоНазначения.СкопироватьРекурсивно(ПараметрыМонитора, Ложь);
	КонецЕсли;
	
	Возврат ПараметрыМонитора;
	
КонецФункции

Функция МониторГотовКРаботе()
	
	Возврат НЕ ПолучитьОсновнуюСхемуКомпоновки() = Неопределено
	        И НЕ СтруктураКолонок = Неопределено;

КонецФункции

Процедура ЗаполнитьМаппингыПолейДляВыводаОтчетовИФорм(ПараметрыМонитора)

	ПараметрыМонитора.Вставить("МаппингПолей", ПолучитьМаппингПолейЗапросаПолученияСпискаОшибок());
	ПараметрыМонитора.Вставить("МаппингПолейСтатистикаОбщая", ПолучитьМаппингПолейЗапросаОбщаяСтатистика());

КонецПроцедуры

Функция ПолучитьОсновнуюСхемуКомпоновки()
	
	Если ЗначениеЗаполнено(АдресСхемы) И ЭтоАдресВременногоХранилища(АдресСхемы) Тогда
		Возврат ПолучитьИзВременногоХранилища(АдресСхемы);
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ПодготовитьМониторКРаботе(Знач Параметры = Неопределено) 
	
	СтруктураКолонок = Новый Структура;
	СтруктураКолонок.Вставить("ОбщаяТаблицаОшибок", СтруктураКолонокТаблицыИзЗаголовкаОшибки(Параметры["МаппингПолей"]));
	СтруктураКолонок.Вставить("СтатистикаПоВсемОшибкам", СтруктураКолонокТаблицыСтатистикаПоВсемОшибкам(Параметры["МаппингПолейСтатистикаОбщая"]));
	
	ПолучитьСхемыКомпоновкиОтчетовИПоместитьВоВременноеХранилище(Параметры);
		
КонецПроцедуры

#КонецОбласти

#Область ФормированиеОтчетаОбОшибках

// Данные монитора из коллектора шаблон.
// 
// Возвращаемое значение:
//  Структура - Данные монитора из коллектора шаблон:
// * ДокументПоВсемОшибкам - Неопределено - 
// 
Функция ДанныеМонитораИзКоллектораШаблон()
	
	Результат = Новый Структура;
	Результат.Вставить("ДокументПоВсемОшибкам", Неопределено);
	Результат.Вставить("ДокументСтатистикаПоВсемОшибкам", Неопределено);	
	
	Возврат Результат;

КонецФункции

Функция СформироватьДокументСтатистикаПоВсемОшибкам(СтатистикаПоОшибкамКакМассив, ПараметрыМонитора)
	
	ДокументСтатистикаПоВсемОшибкам = Новый ТабличныйДокумент;
	ДанныеРасшифроки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; 
	
	Схема = ПолучитьИзВременногоХранилища(АдресСхемыСтатистикаПоВсемОшибкам);   
	НастройкиКомпоновки = ПолучитьИзВременногоХранилища(ПараметрыМонитора["НастройкиСКДДляДокументаОбщаяяСтатистикаОшибок"]);  
	
	ИзменитьСхемуВЗависимостиОтНастроекКомпоновки(Схема, НастройкиКомпоновки);
		
	МакетКомпоновки = КомпоновщикМакета
							.Выполнить(Схема, 
	                                   НастройкиКомпоновки, 
									   ДанныеРасшифроки);
	
	ВнешниеНаборы = Новый Структура("СтатистикаПоВсемОшибкам", 
	                                СтатистикаПоВсемОшибкамВТаблице(СтатистикаПоОшибкамКакМассив));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифроки);
		
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументСтатистикаПоВсемОшибкам);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки); 

	ПоместитьВоВременноеХранилище(ДанныеРасшифроки, АдресДанныхРасшифровкиСтатистикаПоВсемОшибкам);	
	
	Возврат ДокументСтатистикаПоВсемОшибкам;
	
КонецФункции 

Функция СформироватьДокументПоВсемОшибкам(ЗаголовкиВсехОтчетов, ПараметрыМонитора)
	
	ОбщийДокументПоВсемОшибкам = Новый ТабличныйДокумент;
	ДанныеРасшифроки = Новый ДанныеРасшифровкиКомпоновкиДанных;
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; 
	
	Схема = ПолучитьИзВременногоХранилища(АдресСхемы);   
	НастройкиКомпоновки = ПолучитьИзВременногоХранилища(ПараметрыМонитора["НастройкиСКДДляДокументаПоВсемОшибкамСтрока"]);  
	
	ИзменитьСхемуВЗависимостиОтНастроекКомпоновки(Схема, НастройкиКомпоновки);
	
	МакетКомпоновки = КомпоновщикМакета
							.Выполнить(Схема, 
	                                   НастройкиКомпоновки, 
									   ДанныеРасшифроки);
	
	ВнешниеНаборы = Новый Структура("ОбщаяТаблицаОшибок", ЗаголовкиВсехОтчетовОбОшибкахВТаблице(ЗаголовкиВсехОтчетов));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборы, ДанныеРасшифроки);
		
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ОбщийДокументПоВсемОшибкам);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки); 

	ПоместитьВоВременноеХранилище(ДанныеРасшифроки, АдресДанныхРасшифровки);	
	
	Возврат ОбщийДокументПоВсемОшибкам;
	
КонецФункции 

// Изменить схему взависимости от настроек компоновки.
// 
// Параметры:
//  Схема - СхемаКомпоновкиДанных - 
//  НастройкиКомпоновки - НастройкиКомпоновкиДанных - 
Процедура ИзменитьСхемуВЗависимостиОтНастроекКомпоновки(Схема, НастройкиКомпоновки)

	ОписаниеИсключаемыхПолей = Новый Массив;
	НайтиВсеПоляГруппировкиВСтруктуре(НастройкиКомпоновки.Структура, ОписаниеИсключаемыхПолей);
	ИсключитьПоляИзМакетовПолейСхемы(ОписаниеИсключаемыхПолей, Схема);
	
КонецПроцедуры

Процедура НайтиВсеПоляГруппировкиВСтруктуре(Структура, ПоляГруппировки)
	
	Для Каждого ЭлементСтруктуры Из Структура Цикл     
		МассивПолейУровня = Новый Массив;
		Если ЭлементСтруктуры.ПоляГруппировки.Элементы.Количество() > 0 Тогда
			Для Каждого ПолеГруппировки Из ЭлементСтруктуры.ПоляГруппировки.Элементы Цикл
				МассивПолейУровня.Добавить(ПолеГруппировки.Поле);	
			КонецЦикла;
		КонецЕсли;
		ПоляГруппировки.Добавить(МассивПолейУровня);
		НайтиВсеПоляГруппировкиВСтруктуре(ЭлементСтруктуры.Структура, ПоляГруппировки)
	КонецЦикла;
	
КонецПроцедуры 

Процедура ИсключитьПоляИзМакетовПолейСхемы(ОписаниеИсключаемыхПолей, Схема)
	
	ИсключаемыеПоля = Новый Массив;
	Для Каждого ОписаниеПолей Из ОписаниеИсключаемыхПолей Цикл 
		Если ОписаниеПолей.Найти(Новый ПолеКомпоновкиДанных( СтруктураКолонок["ОбщаяТаблицаОшибок"]["ИмяКолонкиИД"])) = Неопределено Тогда
			Для Каждого ПолеКомпоновки Из ОписаниеПолей Цикл
				ИсключаемыеПоля.Добавить(ПолеКомпоновки);	
			КонецЦикла;
		КонецЕсли;
	КонецЦикла; 
	
	ИсключаемыеМакеты = Новый Массив;
	ИсключаемыеПоля = ОбщегоНазначенияКлиентСервер.СвернутьМассив(ИсключаемыеПоля); 
	Для Каждого ИсключаемоеПоле Из ИсключаемыеПоля Цикл
		Для Каждого Макет Из Схема.МакетыПолей Цикл
			Если Новый ПолеКомпоновкиДанных(Макет.Поле)= ИсключаемоеПоле Тогда
				ИсключаемыеМакеты.Добавить(Макет);	
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;                                   
	
	Для Каждого ИсключаемыйМакет Из ИсключаемыеМакеты Цикл
		Схема.МакетыПолей.Удалить(ИсключаемыйМакет);
	КонецЦикла;
	
КонецПроцедуры

Функция СтруктураКолонокТаблицыСтатистикаПоВсемОшибкам(МаппингПолей) 
	
	СоотвествиеИмен = YellowCollectorОбщегоНазначения.СоответствиеИменДляЗапросаОбщаяСтатистикаОшибок();
	Возврат СтруктураКолонокТаблицыКолллектораПоМаппингуИСоотвествиюИмен(МаппингПолей, СоотвествиеИмен);
	
КонецФункции

// Структура колонок таблицы из заголовка ошибки.
// 
// Параметры:
//  МаппингПолей -  Соответствие - 
// 
// Возвращаемое значение:
//  ФиксированнаяСтруктура - Структура колонок таблицы из заголовка ошибки:
// * ОбратноеСоответствиеИмен - ФиксированноеСоответствие -
// * ОписанияКолонок - ФиксированныйМассив -
// * ПрямоеСоотвествиеИмен - ФиксированноеСоответствие -
Функция СтруктураКолонокТаблицыИзЗаголовкаОшибки(МаппингПолей)
	
	СоотвествиеИмен = YellowCollectorОбщегоНазначения.СоответствиеИменДляЗапросаЗаголовкиВсехОтчетовОбОшибках();
	Возврат СтруктураКолонокТаблицыКолллектораПоМаппингуИСоотвествиюИмен(МаппингПолей, СоотвествиеИмен);
	
КонецФункции

Функция СтруктураКолонокТаблицыКолллектораПоМаппингуИСоотвествиюИмен(МаппингПолей, СоотвествиеИмен)
	
	СтруктураКолонокОбщаяТаблицаОшибок = Новый Структура;
	ОписанияКолонок = Новый Массив;
	ОбратноеСоответствиеИмен = Новый Соответствие;
	ИмяКолонкиИД = "";
	
	Для Каждого МаппингПоля Из МаппингПолей Цикл
		
		ОписаниеКолонки = Новый Структура("ИмяКолонки, Заголовок, ТипСтрокой");
		СоответствиеЗаголовка = СоотвествиеИмен.Получить(МаппингПоля["name"]);
		Если СоответствиеЗаголовка = Неопределено Тогда
			
			ОписаниеКолонки.ИмяКолонки = МаппингПоля["name"];
			ОписаниеКолонки.Заголовок = МаппингПоля["name"];
			ОписаниеКолонки.ТипСтрокой = МаппингПоля["type"];
							
		Иначе
			
			ОписаниеКолонки.ИмяКолонки = СоответствиеЗаголовка.Имя;
			ОписаниеКолонки.Заголовок = СоответствиеЗаголовка.Заголовок;
			ОписаниеКолонки.ТипСтрокой = СоответствиеЗаголовка.Тип;

		КонецЕсли;
		
		Если МаппингПоля["id"] = Истина Тогда
			ИмяКолонкиИД = ОписаниеКолонки.ИмяКолонки;
		КонецЕсли;
		
		ОбратноеСоответствиеИмен.Вставить(ОписаниеКолонки.ИмяКолонки, МаппингПоля["name"]);
		
		ОписанияКолонок.Добавить(ОписаниеКолонки);
		
	КонецЦикла;
	
	//СтруктураКолонок.Вставить("ОписанияКолонок", ОбщегоНазначения.ПеревернутьМассив(ОписанияКолонок));
	СтруктураКолонокОбщаяТаблицаОшибок.Вставить("ОписанияКолонок", ОписанияКолонок);
	СтруктураКолонокОбщаяТаблицаОшибок.Вставить("ПрямоеСоотвествиеИмен", СоотвествиеИмен);
	СтруктураКолонокОбщаяТаблицаОшибок.Вставить("ОбратноеСоответствиеИмен", ОбратноеСоответствиеИмен);
	СтруктураКолонокОбщаяТаблицаОшибок.Вставить("ИмяКолонкиИД", ИмяКолонкиИД);
	
	Возврат ОбщегоНазначения.ФиксированныеДанные(СтруктураКолонокОбщаяТаблицаОшибок);
	
КонецФункции

Процедура ПолучитьСхемыКомпоновкиОтчетовИПоместитьВоВременноеХранилище(Параметры)
	
	#Область ОбщаяТаблицаОшибок
	
	Схема = СхемаКомпоновкиОбщая(СтруктураКолонок["ОбщаяТаблицаОшибок"].ОписанияКолонок);	
	АдресСхемы = ПоместитьВоВременноеХранилище(Схема, Параметры["УИДДляВременногоХранилищаАдресаСхемы"]);
	АдресДанныхРасшифровки = ПоместитьВоВременноеХранилище(Неопределено, Параметры["УИДДляВременногоХранилищаАдресаСхемы"]);
	
	#КонецОбласти	
	
	#Область ОбщаяТаблицаОшибок
	
	Схема = СхемаКомпоновкиСтатистикаПоВсемОшибкам(СтруктураКолонок["СтатистикаПоВсемОшибкам"].ОписанияКолонок);	
	АдресСхемыСтатистикаПоВсемОшибкам = ПоместитьВоВременноеХранилище(Схема, 
	                                                                  Параметры["УИДДляВременногоХранилищаАдресаСхемы"]);
	АдресДанныхРасшифровкиСтатистикаПоВсемОшибкам = ПоместитьВоВременноеХранилище(Неопределено, 
	                                                                              Параметры["УИДДляВременногоХранилищаАдресаСхемы"]);
	
	#КонецОбласти
	
КонецПроцедуры

#Область ФормированиеСхемыСКД 

Функция СхемаКомпоновкиСтатистикаПоВсемОшибкам(ОписаниеКолонок)
	
	Схема = СхемаКомпоновкиОбщаяяПоОписаниюКолонокИИмениОбъектаНабора(ОписаниеКолонок, "СтатистикаПоВсемОшибкам");
	ЗаполнитьПредопределенныеПараметрыСхемыСтатистикаПоВсемОшибкам(Схема);
	
	Возврат Схема;
	
КонецФункции

// Схема компоновки общая.
// 
// Параметры:
//  ОписаниеКолонок - ФиксированнаяСтруктура - см. Обработки.МониторОшибок.СтруктураКолонокТаблицыИзЗаголовкаОшибки
// 
// Возвращаемое значение:
//  СхемаКомпоновкиДанных - 
Функция СхемаКомпоновкиОбщая(ОписаниеКолонок)
	
	Схема = СхемаКомпоновкиОбщаяяПоОписаниюКолонокИИмениОбъектаНабора(ОписаниеКолонок, "ОбщаяТаблицаОшибок");
	ЗаполнитьСортировкуСхемыОбщейТаблицыОшибок(Схема);
	ЗаполнитьПредопределенныеПараметрыСхемыОбщейТаблицыОшибок(Схема); 
	ЗаполнитьПредопределенноеУсловноеОформлениеОбщейТаблицыОшибок(Схема);
	
	Возврат Схема;
	
КонецФункции

Функция СхемаКомпоновкиОбщаяяПоОписаниюКолонокИИмениОбъектаНабора(ОписаниеКолонок, ИмяОбъектаНабора)

	Схема = СоздатьСхемуКомпоновкиДанныхСНаборомДанныхОбъект("НаборДанных1", ИмяОбъектаНабора); 
	ЗаполнитьПоляНабораДанных(Схема, ОписаниеКолонок);  
	ЗаполнитьПоляВыбораНастроек(Схема.НастройкиПоУмолчанию, ОписаниеКолонок);
    ДобавитьДетальныеЗаписиСАвтополоемВНастройки(Схема.НастройкиПоУмолчанию);
	ЗаполнитьМакеты(Схема, ОписаниеКолонок, ИмяОбъектаНабора);
	ЗаполнитьМакетыПолей(Схема, ОписаниеКолонок);  
	
	Возврат Схема;
	
КонецФункции


Процедура ЗаполнитьПредопределенноеУсловноеОформлениеОбщейТаблицыОшибок(Схема)
	
	ЭлементыУсловноеОформление = Схема.НастройкиПоУмолчанию.УсловноеОформление.Элементы;
	
	// Фон помеченных на удаления ошибок
	ЭлементОформления = ЭлементыУсловноеОформление.Добавить();
	ЭлементОформления.Использование = Истина;
	
	ЭлементОтбора = ЭлементОформления.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.Использование = Истина;
	ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ПометкаУдаления");
	ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.ПравоеЗначение = Истина;
	
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("ЦветФона", WebЦвета.Розовый);
	
	ЭлементОформления.ИдентификаторПользовательскойНастройки = "ВыделятьПомеченные";
	
	// Граница заголовков
	ЭлементОформления = ЭлементыУсловноеОформление.Добавить();
	ЭлементОформления.Использование = Истина;    
	
	ЭлементОформления.ИспользоватьВЗаголовке = ИспользованиеУсловногоОформленияКомпоновкиДанных.Использовать;
		
	ЭлементОформления.Оформление.УстановитьЗначениеПараметра("СтильГраницы", Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1));
	
КонецПроцедуры

Процедура ЗаполнитьСортировкуСхемыОбщейТаблицыОшибок(Схема)
	
	ЭлементПорядкаКомпоновкиДанныхИД = Схема.НастройкиПоУмолчанию.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));  
	ЭлементПорядкаКомпоновкиДанныхИД.Использование = Истина;      
	ЭлементПорядкаКомпоновкиДанныхИД.РежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ;
	ЭлементПорядкаКомпоновкиДанныхИД.Поле = Новый ПолеКомпоновкиДанных("ИД"); 
	
	Схема.НастройкиПоУмолчанию.Порядок.ИдентификаторПользовательскойНастройки = "Порядок";
	
КонецПроцедуры

// Заполнить предопределнные отборы.
// 
// Параметры:
//  Схема - СхемаКомпоновкиДанных - Схема
Процедура ЗаполнитьПредопределенныеПараметрыСхемыОбщейТаблицыОшибок(Схема)  
	
	// Параметр "Включать помеченные на удаление"
	ПараметрВключатьПомеченныеНаУдаление = Схема.Параметры.Добавить();
	ПараметрВключатьПомеченныеНаУдаление.Имя = "ВключатьПомеченныеНаУдаление";
	ПараметрВключатьПомеченныеНаУдаление.ТипЗначения = Новый ОписаниеТипов("Булево");
	ПараметрВключатьПомеченныеНаУдаление.Заголовок = "Включать помеченные на удаления";
	ПараметрВключатьПомеченныеНаУдаление.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	ПараметрВключатьПомеченныеНаУдаление.ВключатьВДоступныеПоля = Истина;
	ПараметрВключатьПомеченныеНаУдаление.Значение = Истина;
		  
	ЗначениеПараметрВключатьПомеченныеНаУдаление = Схема.НастройкиПоУмолчанию.ПараметрыДанных.Элементы.Добавить();  
	ЗначениеПараметрВключатьПомеченныеНаУдаление.Параметр = Новый ПараметрКомпоновкиДанных("ВключатьПомеченныеНаУдаление");
	ЗначениеПараметрВключатьПомеченныеНаУдаление.Использование = Истина;
    ЗначениеПараметрВключатьПомеченныеНаУдаление.Значение = Истина;
  
	ЗначениеПараметрВключатьПомеченныеНаУдаление.ИдентификаторПользовательскойНастройки = "ВключатьПомеченныеНаУдаление";  
	
	// Параметр "Период"
	ПараметрПериод = Схема.Параметры.Добавить();
	ПараметрПериод.Имя = "Период";
	ПараметрПериод.ТипЗначения = Новый ОписаниеТипов("СтандартныйПериод");
	ПараметрПериод.Заголовок = "Период";
	ПараметрПериод.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	ПараметрПериод.ВключатьВДоступныеПоля = Истина;
	
	ПериодЗначение = Новый СтандартныйПериод;
	ПериодЗначение.ДатаНачала = НачалоДня(ТекущаяДатаСеанса());
	ПериодЗначение.ДатаОкончания = КонецДня(ТекущаяДатаСеанса());
	
	ЗначениеПараметрПериод = Схема.НастройкиПоУмолчанию.ПараметрыДанных.Элементы.Добавить();  
	ЗначениеПараметрПериод.Параметр = Новый ПараметрКомпоновкиДанных("Период");
	ЗначениеПараметрПериод.Использование = Истина;
    ЗначениеПараметрПериод.Значение = ПериодЗначение;
  
	ЗначениеПараметрПериод.ИдентификаторПользовательскойНастройки = "Период";  
	  
КонецПроцедуры 

// Заполнить предопределнные отборы.
// 
// Параметры:
//  Схема - СхемаКомпоновкиДанных - Схема
Процедура ЗаполнитьПредопределенныеПараметрыСхемыСтатистикаПоВсемОшибкам(Схема)  
		
	// Параметр "Период"
	ПараметрПериод = Схема.Параметры.Добавить();
	ПараметрПериод.Имя = "Период";
	ПараметрПериод.ТипЗначения = Новый ОписаниеТипов("СтандартныйПериод");
	ПараметрПериод.Заголовок = "Период";
	ПараметрПериод.Использование = ИспользованиеПараметраКомпоновкиДанных.Всегда;
	ПараметрПериод.ВключатьВДоступныеПоля = Истина;
	
	ПериодЗначение = Новый СтандартныйПериод;
	ПериодЗначение.ДатаНачала = НачалоДня(ТекущаяДатаСеанса());
	ПериодЗначение.ДатаОкончания = КонецДня(ТекущаяДатаСеанса());
	
	ЗначениеПараметрПериод = Схема.НастройкиПоУмолчанию.ПараметрыДанных.Элементы.Добавить();  
	ЗначениеПараметрПериод.Параметр = Новый ПараметрКомпоновкиДанных("Период");
	ЗначениеПараметрПериод.Использование = Истина;
    ЗначениеПараметрПериод.Значение = ПериодЗначение;
  
	ЗначениеПараметрПериод.ИдентификаторПользовательскойНастройки = "Период";  
	  
КонецПроцедуры

Процедура ЗаполнитьМакеты(Схема, ОписаниеКолонок, ИмяОбъектаНабора)
	
	Для Каждого ОписаниеКолонки Из ОписаниеКолонок Цикл
		
		ОписаниеМакета = ДобавитьОписаниеМакета(Схема, ОписаниеКолонки.ИмяКолонки);
		ДобавитьПараметрыВОписаниеМакета(ОписаниеМакета, ОписаниеКолонки.ИмяКолонки, ИмяОбъектаНабора);
		ДобавитьМакетВОписаниеМакета(ОписаниеМакета, ОписаниеКолонки.ИмяКолонки)
		
	КонецЦикла;
	
КонецПроцедуры

// Добавить макет в описание макета.
// 
// Параметры:
//  ОписаниеМакета - ОписаниеМакетаСхемыКомпоновкиДанных - Описание макета
//  ИмяПоля - Строка -
Процедура ДобавитьМакетВОписаниеМакета(ОписаниеМакета, ИмяПоля)
	
	Макет = Новый МакетОбластиКомпоновкиДанных; 
	
	СтрокаТаблицыОбласти = Макет.Добавить(Тип("СтрокаТаблицыОбластиКомпоновкиДанных")); 
	Ячейка = СтрокаТаблицыОбласти.Ячейки.Добавить();
	
	ПолеОбластиКомпоновкиДанных = Ячейка.Элементы.Добавить(Тип("ПолеОбластиКомпоновкиДанных"));
	ПолеОбластиКомпоновкиДанных.Значение = Новый ПараметрКомпоновкиДанных(ИмяПоля);  
	
	Ячейка.Оформление.УстановитьЗначениеПараметра("Расшифровка", Новый ПараметрКомпоновкиДанных("Расшифровка")); 
	Ячейка.Оформление.УстановитьЗначениеПараметра("СтильГраницы", Новый Линия(ТипЛинииЯчейкиТабличногоДокумента.Сплошная, 1)); 
	
	
	ОписаниеМакета.Макет = Макет; 
	
КонецПроцедуры

// Добавить параметры в описание макета.
// 
// Параметры:
//  ОписаниеМакета - ОписаниеМакетаСхемыКомпоновкиДанных - 
//  ИмяКолонки - Строка -
Процедура ДобавитьПараметрыВОписаниеМакета(ОписаниеМакета, ИмяКолонки, Знач ИмяОбъектаНабора = Неопределено)
	
	// Параметр выражения представляет собой описание выражения, которое будет выводиться в макете
	ПараметрВыражение = ОписаниеМакета.Параметры.Добавить(Тип("ПараметрОбластиВыражениеКомпоновкиДанных"));
	ПараметрВыражение.Выражение = ИмяКолонки;
	ПараметрВыражение.Имя = ИмяКолонки; 
	
	
	ПараметрРасшифровка = ОписаниеМакета.Параметры.Добавить(Тип("ПараметрОбластиРасшифровкаКомпоновкиДанных"));  
	ПараметрРасшифровка.Имя = "Расшифровка";
	
	Если ИмяОбъектаНабора = "ОбщаяТаблицаОшибок" Тогда
		
		// В каждом поле необходимо хранить ИД отчета об ошибке, чтобы понимать к какому отчету относится та или иная ошибка
		ВыражениеИД= ПараметрРасшифровка.ВыраженияПолей.Добавить();
		ВыражениеИД.Выражение = СтруктураКолонок["ОбщаяТаблицаОшибок"]["ИмяКолонкиИД"];
		ВыражениеИД.Поле = СтруктураКолонок["ОбщаяТаблицаОшибок"]["ИмяКолонкиИД"]; 
		
	КонецЕсли;
	
	ВыражениеПоле = ПараметрРасшифровка.ВыраженияПолей.Добавить();
	ВыражениеПоле.Выражение = ИмяКолонки;
	ВыражениеПоле.Поле = ИмяКолонки; 	
	
КонецПроцедуры   

// Добавить описание макета.
// 
// Параметры:
//  Схема - СхемаКомпоновкиДанных - 
//  ИмяПоля - Строка - 
// 
// Возвращаемое значение:
//  ОписаниеМакетаСхемыКомпоновкиДанных - 
Функция ДобавитьОписаниеМакета(Схема, ИмяПоля)
	
	ОписаниеМакета =  Схема.Макеты.Добавить();  
	ОписаниеМакета.Имя = ИмяМакетаПоля(ИмяПоля); 
	
	Возврат ОписаниеМакета;
	
КонецФункции

// Заполнить макеты полей.
// 
// Параметры:
//  Схема - СхемаКомпоновкиДанных - Схема
//  ОписаниеКолонок - ФиксированнаяСтруктура - см. Обработки.МониторОшибок.СтруктураКолонокТаблицыИзЗаголовкаОшибки
//
Процедура ЗаполнитьМакетыПолей(Схема, ОписаниеКолонок)
	
	Для Каждого ОписаниеКолонки Из ОписаниеКолонок Цикл
		СвязатьМакетПоляСМакетом(Схема, ОписаниеКолонки.ИмяКолонки);
	КонецЦикла;
	
КонецПроцедуры

// Добавить макет поля.
// 
// Параметры:
//  СхемаКомпоновкиДанных - СхемаКомпоновкиДанных - 
//  ИмяПоля - Строка - 
Процедура СвязатьМакетПоляСМакетом(СхемаКомпоновкиДанных, ИмяПоля)

	МакетПоля = СхемаКомпоновкиДанных.МакетыПолей.Добавить();
	МакетПоля.Макет = ИмяМакетаПоля(ИмяПоля);
	МакетПоля.Поле = ИмяПоля;
	
КонецПроцедуры

// Имя макета поля.
// 
// Параметры:
//  ИмяПоля - Строка - 
// 
// Возвращаемое значение:
//  Строка - Имя макета поля
Функция ИмяМакетаПоля(ИмяПоля)
	Возврат СтрШаблон("%1%2", "Макет", ИмяПоля);
КонецФункции

Процедура ЗаполнитьПоляНабораДанных(Схема, ОписанияКолонок)
	
	Для Каждого ОписаниеКолонки Из ОписанияКолонок Цикл

		ДобавитьПолеВНаборДанных(Схема.НаборыДанных[0], 
		                         ОписаниеКолонки.ИмяКолонки, 
		                         ОписаниеКолонки.ИмяКолонки,
			                     ОписаниеКолонки.Заголовок,
							     ОписаниеКолонки.ТипСтрокой);
			
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьПоляВыбораНастроек(Настройки, ОписанияКолонок)
	
	Для Каждого ОписаниеКолонки Из ОписанияКолонок Цикл

		ВыбранноеПоле = Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПоле.Использование = Истина;
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(ОписаниеКолонки.ИмяКолонки);

	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьДетальныеЗаписиСАвтополоемВНастройки(Настройки)
	
	ДетальныеЗаписи = Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ДетальныеЗаписи.Использование = Истина;
	   
	Автополе = ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	Автополе.Использование = Истина; 
	
	Порядок = ДетальныеЗаписи.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
КонецПроцедуры

#Область РаботаССКД

Функция СоздатьСхемуКомпоновкиДанныхСНаборомДанныхОбъект(ИмяНабора, ИмяОбъекта)

	СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;

	ИсточникДанных = СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
	ИсточникДанных.Имя = "ИсточникДанных1";
	ИсточникДанных.ТипИсточникаДанных = "Local";

	НаборДанныхОбъект = СхемаКомпоновкиДанных
	                                         .НаборыДанных
	                                         .Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
	
	НаборДанныхОбъект.Имя = ИмяНабора;
	НаборДанныхОбъект.ИмяОбъекта = ИмяОбъекта;
	НаборДанныхОбъект.ИсточникДанных = "ИсточникДанных1";
	
	Возврат СхемаКомпоновкиДанных;

КонецФункции

Функция ДобавитьПолеВНаборДанных(Набор, ИмяПоля, Путь, Заголовок, ТипСтрокой)

	ПолеНабора = Набор.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабора.Поле = ИмяПоля;
	ПолеНабора.ПутьКДанным = Путь;
	ПолеНабора.Заголовок = Заголовок;
	ПолеНабора.ТипЗначения = Новый ОписаниеТипов(ТипСтрокой);
	
	Возврат ПолеНабора;

КонецФункции

#КонецОбласти    

#КонецОбласти

#Область Вспомогательные

Функция СтатистикаПоВсемОшибкамВТаблице(ЗаголовкиОшибок)
	
	ОбщаяТаблицаОшибок = Новый ТаблицаЗначений;
	Для Каждого ОписаниеКолонки Из СтруктураКолонок["СтатистикаПоВсемОшибкам"].ОписанияКолонок Цикл
		ОбщаяТаблицаОшибок.Колонки.Добавить(ОписаниеКолонки.ИмяКолонки);	
	КонецЦикла;
	
	Для Каждого ЗаголовокОшибки Из ЗаголовкиОшибок Цикл
		
		СтрокаОписаниеОшибки = ОбщаяТаблицаОшибок.Добавить();   
		Для Каждого ОписаниеКолонки Из СтруктураКолонок["СтатистикаПоВсемОшибкам"].ОписанияКолонок Цикл
			
			ЗначениеПоляЗаголовкаВСтроке = ЗаголовокОшибки.Получить(СтруктураКолонок["СтатистикаПоВсемОшибкам"].ОбратноеСоответствиеИмен.Получить(ОписаниеКолонки.ИмяКолонки));
			Если НРег(ОписаниеКолонки.ТипСтрокой) = "дата" Тогда
				ЗначениеПоляЗаголовка = ПрочитатьДатуJSON(ЗначениеПоляЗаголовкаВСтроке, ФорматДатыJSON.ISO); 
			Иначе
				ЗначениеПоляЗаголовка = ЗначениеПоляЗаголовкаВСтроке; 	
			КонецЕсли;                                                
			
			СтрокаОписаниеОшибки[ОписаниеКолонки.ИмяКолонки] = ЗначениеПоляЗаголовка;
						  
		КонецЦикла;	  
		
	КонецЦикла;
	
	Возврат ОбщаяТаблицаОшибок;
		
КонецФункции

Функция ЗаголовкиВсехОтчетовОбОшибкахВТаблице(ЗаголовкиОшибок)
	
	ОбщаяТаблицаОшибок = Новый ТаблицаЗначений;
	Для Каждого ОписаниеКолонки Из СтруктураКолонок["ОбщаяТаблицаОшибок"].ОписанияКолонок Цикл
		ОбщаяТаблицаОшибок.Колонки.Добавить(ОписаниеКолонки.ИмяКолонки);	
	КонецЦикла;
	
	Для Каждого ЗаголовокОшибки Из ЗаголовкиОшибок Цикл
		
		СтрокаОписаниеОшибки = ОбщаяТаблицаОшибок.Добавить();   
		Для Каждого ОписаниеКолонки Из СтруктураКолонок["ОбщаяТаблицаОшибок"].ОписанияКолонок Цикл
			
			ЗначениеПоляЗаголовкаВСтроке = ЗаголовокОшибки.Получить(СтруктураКолонок["ОбщаяТаблицаОшибок"].ОбратноеСоответствиеИмен.Получить(ОписаниеКолонки.ИмяКолонки));
			Если НРег(ОписаниеКолонки.ТипСтрокой) = "дата" Тогда
				ЗначениеПоляЗаголовка = ПрочитатьДатуJSON(ЗначениеПоляЗаголовкаВСтроке, ФорматДатыJSON.ISO); 
			Иначе
				ЗначениеПоляЗаголовка = ЗначениеПоляЗаголовкаВСтроке; 	
			КонецЕсли;                                                
			
			СтрокаОписаниеОшибки[ОписаниеКолонки.ИмяКолонки] = ЗначениеПоляЗаголовка;
						  
		КонецЦикла;	  
		
	КонецЦикла;
	
	Возврат ОбщаяТаблицаОшибок;
		
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ВзаимодействиеСYellowCollector

Функция ПолучитьМаппингПолейЗапросаПолученияСпискаОшибок()
	
	МаппингПолей = МенеджерМонитора.ПолучитьМаппингПолейЗапросаПолученияСпискаОшибок();
	Если МаппингПолей.Получить("fields") = Неопределено 
	     ИЛИ МаппингПолей["fields"].Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Не удалось связаться с сервером ""Yellow Collector""'");	
	КонецЕсли;  
	Возврат МаппингПолей["fields"];
	
КонецФункции 

Функция ПолучитьМаппингПолейЗапросаОбщаяСтатистика()
	
	МаппингПолей = МенеджерМонитора.ПолучитьМаппингПолейЗапросаОбщаяСтатистика();
	Если МаппингПолей.Получить("fields") = Неопределено 
	     ИЛИ МаппингПолей["fields"].Количество() = 0 Тогда
		ВызватьИсключение НСтр("ru='Не удалось связаться с сервером ""Yellow Collector""'");	
	КонецЕсли;  
	Возврат МаппингПолей["fields"];
	
КонецФункции

Функция ПолучитьЗаголовкиВсехОтчетовОбОшибках(ПараметрыМонитора) 
	
	ПараметрыПолученияЗаголовков = ПараметрыПолученияЗаголовковИзПараметровМонитора(ПараметрыМонитора);
	ЗаголовкиВсехОтчетов = МенеджерМонитора.ПолучитьЗаголовкиВсехОтчетовОбОшибках(ПараметрыПолученияЗаголовков);
	Если ЗаголовкиВсехОтчетов.Получить("headers") = Неопределено  Тогда
		ВызватьИсключение НСтр("ru='Не удалось выполнить корректное обращение к серверу ""Yellow Collector""'");	
	КонецЕсли;  
	Возврат ЗаголовкиВсехОтчетов["headers"];

КонецФункции  

Функция СтатистикаПоОшибкамОбщаяКакМассив(ПараметрыМонитора)
	
	ПараметрыПолученияЗаголовков = ПараметрыПолученияЗаголовковИзПараметровМонитора(ПараметрыМонитора);
	СтатистикаПоОшибкамОбщая = МенеджерМонитора.СтатистикаПоОшибкамОбщая(ПараметрыПолученияЗаголовков);
	Если СтатистикаПоОшибкамОбщая.Получить("elements") = Неопределено  Тогда
		ВызватьИсключение НСтр("ru='Не удалось выполнить корректное обращение к серверу ""Yellow Collector""'");
	КонецЕсли;  
	Возврат СтатистикаПоОшибкамОбщая["elements"];
	
КонецФункции

Функция ПараметрыПолученияЗаголовковИзПараметровМонитора(ПараметрыМонитора)
	
	ПараметрыПолученияЗаголовоков = ПараметрыПолученияЗаголовковШаблон();
	Если НЕ (ПараметрыМонитора.Свойство("НастройкиСКДДляДокументаПоВсемОшибкамСтрока") 
		И ЭтоАдресВременногоХранилища(ПараметрыМонитора.НастройкиСКДДляДокументаПоВсемОшибкамСтрока)) Тогда
		Возврат ПараметрыПолученияЗаголовоков;
	КонецЕсли;
	
	Настройки = ПолучитьИзВременногоХранилища(ПараметрыМонитора.НастройкиСКДДляДокументаПоВсемОшибкамСтрока);	
	ПараметрВключатьПомеченныеНаУдаление = Настройки
						                    .ПараметрыДанных
											   .НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВключатьПомеченныеНаУдаление"));
											   
	Если НЕ ПараметрВключатьПомеченныеНаУдаление = Неопределено Тогда		
		ПараметрыПолученияЗаголовоков["includeDeletedMarkReports"] = ПараметрВключатьПомеченныеНаУдаление.Значение;			
	КонецЕсли;
	
	ПараметрПериод = Настройки
					  .ПараметрыДанных
                        .НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Период"));
											   
	Если НЕ ПараметрПериод = Неопределено Тогда		
		
		ПараметрыПолученияЗаголовоков["from"] = ПараметрПериод.Значение.ДатаНачала;         
		ПараметрыПолученияЗаголовоков["to"] = ПараметрПериод.Значение.ДатаОкончания; 
		
	КонецЕсли;                                                              
				
	Возврат ПараметрыПолученияЗаголовоков;
	
КонецФункции  

Функция ПараметрыПолученияЗаголовковШаблон()
	
	ПараметрыПолученияЗаголовоков = Новый Структура;
	ПараметрыПолученияЗаголовоков.Вставить("from", НачалоДня(ТекущаяДатаСеанса()));         
	ПараметрыПолученияЗаголовоков.Вставить("to", КонецДня(ТекущаяДатаСеанса()));   
	ПараметрыПолученияЗаголовоков.Вставить("includeDeletedMarkReports", Истина);
	
	Возврат ПараметрыПолученияЗаголовоков;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Инициализация 

МенеджерМонитора = Обработки.МониторОшибок;

#КонецОбласти	
	
#Иначе
ВызватьИсключение НСтр("ru='Недопустимый вызов объекта на клиенте'");
#КонецЕсли